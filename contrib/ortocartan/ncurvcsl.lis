(setq infoncurva (QUOTE (THIS IS THE PROGRAM FOR CALCULATING THE CURVATURE
TENSOR FROM GIVEN (SYMMETRIC) CONNECTION COEFFICIENTS IN ANY NUMBER OF
DIMENSIONS!. IT IS WRITTEN ON TOP OF ORTOCARTAN AND OF ELLISEVOL IN
Codemist Standard LISP to be run under Windows 98 and Linux)))

(de CURVINIT (L NUMBER) (PROG (V ARRAY MEM GLS VE)
    (TERPRI) (TERPRI)
    (PRINTC (CAR L))
    (TERPRI) (TERPRI)
    (MAPC (CDR L) (FUNCTION (LAMBDA (LE) (SELECT (CAR LE)
        ((QUOTE COORDINATES)   (SETQ COORD (CDR LE)))
        ((QUOTE FUNCTIONS)   (SETQ FUNCTIONS (CDR LE)))
        ((QUOTE CONSTANTS)   (SETQ CONSTANTS (CDR LE)))
        ((QUOTE CONNECTION)   (SETQ ARRAY (CDR LE)))
        ((QUOTE SYMBOLS)   (SETQ METSUBS (CDR LE)))
        ((QUOTE SUBSTITUTIONS)   (SETQ SIMPLSUBS (CDR LE)))
        ((QUOTE DONT)   (SETQ NOPRINT (CDDR LE)))
        ((QUOTE STOP)   (SETQ STOP (CADDR LE)))
        ((QUOTE TENSORS)   (SETQ TENSORS (CDR LE)))
        ((QUOTE DATA)   (SETQ DATASUBS (CDDR LE)))
        ((QUOTE EXPAND)   (PROG2 (SETQ LPOWER (CADDDR LE))
                                 (SETQ UPOWER (CADR (CDDDDR LE))) ))
        ((QUOTE MARKERS)   (SETQ MARKERS (CDR LE)))
        ((QUOTE RMARGIN)   (SETQ RMARGIN (CADR LE)))
        ((QUOTE LMARGIN)   (SETQ LMARGIN (CADR LE)))
        ((QUOTE FACTOROUT)   (SETQ FACTORS (CDR LE)))
        ((QUOTE OUTPUT)   (COND
           ((EQUAL LE (QUOTE (OUTPUT FOR INPUT)))
                                     (SETQ OUTINP (QUOTE INPUT)))
           ((EQUAL LE (QUOTE (OUTPUT FOR LATEX)))
                                     (SETQ OUTINP (QUOTE LATEX)))
           (T          (ERROR (LIST (QUOTE "*****///") LE (QUOTE "///*****")
                                    (QUOTE "IS AN ILLEGAL ARGUMENT")
                                    (QUOTE "FOR OUR SYSTEM.")
                                    (QUOTE "SORRY, CAN'T GO ON."))) )))
        (ERROR (LIST (QUOTE "*****///") LE (QUOTE "///*****")
                          (QUOTE "IS AN ILLEGAL ARGUMENT")
                          (QUOTE "FOR OUR SYSTEM.")
                                    (QUOTE "SORRY, CAN'T GO ON")))) )))
    (SETQ V (LENGTH ARRAY))
    (SETQ VE (STIMES (LIST (QUOTE (1 . 2)) (SEXPT NUMBER 2)
                           (ADD1 NUMBER) )))
    (OR (EQN V VE) (ERROR (LIST
        (QUOTE "THE NUMBER OF CONNECTION")
        (QUOTE "COEFFICIENTS IS TOO")
        (COND ((GREATERP V VE)   (QUOTE LARGE))
              (T   (QUOTE SMALL)))
        (QUOTE "COMPARED TO THE DECLARED")
        (QUOTE "DIMENSION OF THE MANIFOLD =") NUMBER (QUOTE !.)
        (QUOTE "PLEASE CORRECT")  )))
    (OR COORD (ERROR (LIST (QUOTE "YOU DID NOT SPECIFY THE NAMES OF")
                           (QUOTE "THE COORDINATES. I CANNOT DO MY")
                           (QUOTE "JOB WITHOUT THIS INFORMATION.")
                           (QUOTE "SORRY, TRY AGAIN PLEASE.")) ))
    (OR (EQN (LENGTH COORD) NUMBER) (ERROR (LIST
        (QUOTE "THE NUMBER OF COORDINATES")
        (QUOTE "DOES NOT MATCH THE DECLARED")
        (QUOTE "DIMENSION OF THE MANIFOLD =") NUMBER (QUOTE !.)
        (QUOTE "PLEASE CORRECT")  )))
    (SETQ MESS (NOT (MEMQ (QUOTE MESSAGES) NOPRINT)))
    (OR LPOWER (SETQ LPOWER 2))
    (OR UPOWER (SETQ UPOWER 3))
    (AND (GREATERP RMARGIN 72) (LINELENGTH RMARGIN))
    (MAP METSUBS (FUNCTION (LAMBDA (ME)
                 (RPLACA ME (INTOPRE (CAR ME))) )))
    (LISTDEPS)
    (DEFSYMB)
    (MAKEPAIRS)
    (PUTSYMB)
    (MAP DATASUBS (FUNCTION (LAMBDA (DE)
         (RPLACA DE (SIMPLIFY (INTOPRE (CAR DE)) T)) )))
    (AND SYMBOLS (PROGN
         (MESSAGE (QUOTE SYMBOLS) T)
         (TURNCAR)
         (MAPC SYMBOLS (FUNCTION (LAMBDA (SE) (PROG2
               (INPRINT SE NIL NIL (QUOTE =) (GET SE (QUOTE CONTENT)))
               (TURNCAR)) ))) ))
    (MAPC MARKERS (FUNCTION (LAMBDA (ME) (SET ME NIL) )))
    (AND SIMPLSUBS (PROG (S M L DGLS M!?)
         (OR (SETQ MEM (MEMQ (QUOTE SUBSTITUTIONS) NOPRINT)) (PROG2
             (MESSAGE (QUOTE SUBSTITUTIONS) T)
             (TURNCAR)))
         (SETQ S (QUOTE =))
         (SETQ V SIMPLSUBS)
         (AND MARKERS (SETQ GLS
              (MAPCAR V (FUNCTION (LAMBDA (VE) (AND (EQ VE S) S) ))) )
              (SETQ DGLS GLS))
       A (OR (SETQ M (FRONT (SETQ L (PREVIOUS S V)) V)) MEM
             (MESSAGE (QUOTE EVERYWHERE) T))
         (SETQ V (MEMBER L V))
         (OR MEM (PROGN
             (MAPC M (FUNCTION (LAMBDA (ME) (PROG2
                (MESSAGE ME NIL)
                (MESSAGE BLANK NIL)) )))
             (TURNCAR) (TURNCAR) ))
         (RPLACA V (INTOPRE (CAR V)))
         (COND (M!?   (PROGN (SETQ DGLS (EQUATE V DGLS))
                            (RPLACA DGLS T)
                            (RPLACA V (SMOOTH (CAR V) T))
                            (SETQ M!? NIL)))
               (T   (RPLACA V (SIMPLIFY (CAR V) T))))
         (RPLACA (CDDR V) (SIMPLIFY (INTOPRE (CADDR V)) T))
         (SETQ M!? NIL)
         (AND (EQ (CADDR V) (QUOTE EXPAND)) (COND
              ((AND (NOT (ATOM (CAR V))) (EQ (CAAR V) (QUOTE EXPT))
                    (CDAR V) (CDDAR V) (NULL (CDDDAR V)))
                        (OR (AND DGLS (CAR DGLS)) (RPLACA (CDDR V)
                                      (DEVELOP (CADAR V) (CADDAR V))) ))
              (T   (ERROR (LIST (QUOTE "THE EXPRESSION *****///")
                      (CAR V) (QUOTE "///***** IS NOT AN EXPONENTIAL")
                      (QUOTE "EXPRESSION, AND SO THE COMMAND")
                      (QUOTE "** EXPAND ** IS MEANINGLESS IN THIS")
                      (QUOTE "CASE. SORRY, MUST QUIT."))) )))
         (OR MEM (PROGN
             (INPRINT (CAR V) NIL NIL S (CADDR V))
             (TURNCAR) (TURNCAR) ))
         (AND (SETQ V (CDDDR V)) (GO A)) ))
    (AND DATASUBS (PROG (GL)
         (PROG ( ) (MESSAGE (QUOTE (THE SUBSTITUTIONS LISTED ABOVE WILL
                            BE THEMSELVES TRANSFORMED BY THE FOLLOWING
                            SUBSTITUTIONS)) T)
                   (SETQ V DATASUBS)
                 A (COND ((EQ (CADR V) (QUOTE =))
                                         (MESSAGE (QUOTE EVERYWHERE) T))
                         (T   (MAPC (FRONT (PREVIOUS (QUOTE =) V) V)
                                    (FUNCTION (LAMBDA (FE) (PROG2
                                    (INPRINT (QUOTE "IN THE VALUE") NIL
                                       NIL (QUOTE "OF THE EXPRESSION")
                                       (CAR V))
                                    (SETQ V (CDR V))) ))) ))
                   (INPRINT (CAR V) NIL NIL (QUOTE =) (CADDR V))
                   (AND (SETQ V (CDDDR V)) (GO A)))
         (SETQ GL (MAPCAR SIMPLSUBS (FUNCTION (LAMBDA (SE)
                          (AND (EQ SE (QUOTE =)) SE) ))) )
         (CLEARDATA)
         (REMPROP (QUOTE CLEARDATA) (QUOTE EXPR))
         (REMPROP (QUOTE EQUATE) (QUOTE EXPR))
         (SETQ DATASUBS NIL)
         (OR (MEMQ (QUOTE MODIFICATIONS) NOPRINT) (PROGN
             (MESSAGE (QUOTE (THE SUBSTITUTIONS YOU ASKED ME TO
                 MODIFY WILL HAVE THE FOLLOWING FINAL FORM)) T)
             (TURNCAR) (TURNCAR) (TURNCAR)
             (PROG (S M L)
                 (SETQ S (QUOTE =))
                 (SETQ V SIMPLSUBS)
              A  (COND ((OR (PREVIOUS S GL) (NEXT S GL)) (PROGN
                            (OR (SETQ M (REMOVELAST (FRONT S V)))
                                (MESSAGE (QUOTE EVERYWHERE) T))
                            (MAPC M (FUNCTION (LAMBDA (ME) (PROGN
                                  (SETQ V (CDR V))
                                  (MESSAGE ME NIL)
                                  (MESSAGE BLANK NIL)) )))
                            (TURNCAR) (TURNCAR)
                            (INPRINT (CAR V) NIL NIL S (CADDR V))
                            (TURNCAR) (TURNCAR)
                            (AND (SETQ V (CDDDR V))
                                 (SETQ GL (CDDR (MEMQ S GL)))
                                 (GO A)) ))
                       (T   (AND (SETQ GL (CDDR (MEMQ S GL)))
                                 (SETQ V (CDDR (MEMQ S V)))
                                 (GO A))) ))) )))
    (SETQ SIMPLSUBS (SUBSTITUTIONS SIMPLSUBS))
    (LISTTENS TENSORS)
    (MAP ARRAY (FUNCTION (LAMBDA (AE)
                         (RPLACA AE (SIMPLIFY (INTOPRE (CAR AE)) T)) )))
    (MAPC (QUOTE (FRACTION POWER RANPIN TERM FACTOR INTOPRETERM
                  MAKETERMS INTOPRESUM INTOPRE NEXT PREVIOUS LISTTENS
                  SUBSTITUTIONS INIT)) (FUNCTION (LAMBDA (QE)
          (REMPROP QE (QUOTE EXPR)) )))
    (SETQ GAM (GENSYM))
    (MKARRAY GAM (LIST NUMBER NUMBER NUMBER))
    (SETQ MEM (MEMQ (QUOTE CONNECTION) NOPRINT))
    (MAPC LIND (FUNCTION (LAMBDA (I) (PROGN
        (RPLACA LIJK I)
        (MAPC LIND (FUNCTION (LAMBDA (J) (PROG2
            (RPLACA (CDR LIJK) J)
            (MAPC (MEMBER J LIND) (FUNCTION (LAMBDA (K) (PROGN
                (RPLACA (CDDR LIJK) K)
                (SETQ V (CAR ARRAY))
                (SUBSALL (QUOTE V)(QUOTE CONNECTION) NIL LIJK SIMPLSUBS)
                (PUTIN GAM LIJK V)
                (OR (EQN V 0) MEM (INPRINT (QUOTE CONNECTION)
                         (QUOTE (+ - -)) LIJK (QUOTE =) V))
                (SETQ ARRAY (CDR ARRAY)) )))) ))))
        (RPLACA LABC I)
        (MAPC (CDR LIND) (FUNCTION (LAMBDA (J) (PROGN
            (RPLACA (CDDR LABC) J)
            (RPLACA (CDR LIJK) J)
            (MAPC LIND (FUNCTION (LAMBDA (K) (AND (GREATERP J K) (PROGN
                (RPLACA (CDR LABC) K)
                (RPLACA (CDDR LIJK) K)
                (PUTIN GAM LIJK (PICKEL GAM LABC)) )) ))) )))) ))))
    (TIMEMESSAGE (QUOTE "CONNECTION COEFFICIENTS") T)
    (TURNCAR) (TURNCAR) (TURNCAR)
    (REMPROP (QUOTE CONNECTION) (QUOTE SUBS)) ))
(de CURVATURE (RANK LIS)
    (PROG (COORD FUNCTIONS CONSTANTS METSUBS SIMPLSUBS NOPRINT
           STOP TENSORS DATASUBS GAM RM RC RINV
           FUNNAMES SYMBOLS UPIE LPOWER UPOWER WORKARR WV LIND LIJ LIJK
           LIJKL LAB LABC LABCD MESS MARKERS LMARGIN CLOCK V OUTINP
           LIJK2 LIJK3 LIJK4 LIJK5 FACTORS)
    (MAPC (OBLIST) (FUNCTION RESETVARS))
    (SETQ CLOCK (TIME))
    (SETQ RMARGIN 72)
    (SETQ LMARGIN  8)
% LMARGIN IS THE MARGIN FOR CONTINUATION LINES ONLY
    (SETQ V (SUB1 RANK))
 A  (SETQ LIND (CONS V LIND))
    (SETQ V (SUB1 V))
    (OR (EQN V -1) (GO A))
    (SETQ LIJ (LIST NIL NIL))
    (SETQ LIJK (LIST NIL NIL NIL))
    (SETQ LIJK2 (COPY LIJK))
    (SETQ LIJK3 (COPY LIJK))
    (SETQ LIJK4 (COPY LIJK))
    (SETQ LIJK5    (COPY LIJK))
    (SETQ LIJKL (LIST NIL NIL NIL NIL))
    (SETQ LAB (LIST NIL NIL))
    (SETQ LABC (LIST NIL NIL NIL))
    (SETQ LABCD (LIST NIL NIL NIL NIL))
    (CURVINIT LIS RANK)
    (RPLACD LIS NIL)
    (NRIEMANN)
    (LINELENGTH 72)
    (TERPRI) (TERPRI)
    (MESSAGE (QUOTE ("I REALLY LIKED THIS!" "CAN I HAVE MORE ?"
                                           "  PLEASE ?!?")) T)
    (TERPRI)
    (MESSAGE (QUOTE "END OF WORK") T)
    (MESSAGE (LIST (QUOTE "RUN TIME =") (PLUS (TIME) (MINUS CLOCK))
                   (QUOTE MSEC)) T)
     ))

(de NPRODUCERIEM (I J K L) (PROG (R V)
    (RPLACA LIJK I)
    (RPLACA (CDR LIJK) J)
    (RPLACA (CDDR LIJK) K)
    (RPLACA LABC I)
    (RPLACA (CDR LABC) J)
    (RPLACA (CDDR LABC) L)
    (RPLACA LIJK2 I)
    (RPLACA (CDDR LIJK2) K)
    (RPLACA (CDR LIJK3) J)
    (RPLACA (CDDR LIJK3) L)
    (RPLACA LIJK4 I)
    (RPLACA (CDDR LIJK4) L)
    (RPLACA (CDR LIJK5) J)
    (RPLACA (CDDR LIJK5) K)
    (SETQ V (SMINUS (DERIV (NTH COORD (ADD1 L)) (PICKEL GAM LIJK))))
    (SETQ V (CONS V NIL))
    (SETQ R (DERIV (NTH COORD (ADD1 K)) (PICKEL GAM LABC)))
    (SETQ V (CONS R V))
    (MAPC LIND (FUNCTION (LAMBDA (LE) (PROGN
        (RPLACA (CDR LIJK2) LE)
        (RPLACA LIJK3 LE)
        (RPLACA (CDR LIJK4) LE)
        (RPLACA LIJK5 LE)
        (SETQ V (CONS (STIMES (LIST
                      (PICKEL GAM LIJK2) (PICKEL GAM LIJK3))) V))
        (SETQ V (CONS (STIMES (LIST -1
                      (PICKEL GAM LIJK4) (PICKEL GAM LIJK5))) V)) ))))
    (SETQ V (SPLUS V))
    (SUBSALL (QUOTE V) (QUOTE NCURVATURE) NIL LIJKL SIMPLSUBS)
    (AND (NOT (EQN V 0)) (SETQ CHECK T) (NOT MEM)
         (INPRINT (QUOTE NCURVATURE) (QUOTE (+ - - -)) LIJKL
                  (QUOTE =) V)
         (TURNCAR))
    (RETURN V) ))
(de NRIEMANN ( ) (PROG (MEM CHECK)
    (SETQ RM (GENSYM))
    (MKARRAY RM (LIST RANK RANK RANK RANK))
    (SETQ MEM (MEMQ (QUOTE NCURVATURE) NOPRINT))
    (MAPC LIND (FUNCTION (LAMBDA (I) (PROG2
        (RPLACA LIJKL I)
        (MAPC LIND (FUNCTION (LAMBDA (J) (PROG2
            (RPLACA (CDR LIJKL) J)
            (MAPC LIND (FUNCTION (LAMBDA (K) (PROG2
                (RPLACA (CDDR LIJKL) K)
                (MAPC (CDR (MEMBER K LIND)) (FUNCTION (LAMBDA (L) (PROG2
                    (RPLACA (CDDDR LIJKL) L)
                    (PUTIN RM LIJKL
                           (NPRODUCERIEM I J K L)) )))) )))) )))) ))))
    (TIMEMESSAGE (QUOTE NCURVATURE) NIL)
    (OR CHECK (PROGN
        (MESSAGE (QUOTE ("ALL COMPONENTS OF THE"
                                   "CURVATURE TENSOR ARE ZERO")) T)
        (TURNCAR) (TURNCAR) (TURNCAR)
        (RETURN NIL)))
    (RETURN CHECK) ))
