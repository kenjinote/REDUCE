(setq infolandlagr (QUOTE (This is the program Landlagr that calculates
the Landau-Lifshitz lagrangian from a given metric tensor!. It will run
only if the Lisp has the programs Ortocartan and Ellisevol already in
core!.)))

(de LANDLAGR (LIS)
    (PROG (COORD FUNCTIONS CONSTANTS EMATRIX METSUBS SIMPLSUBS NOPRINT
           STOP TENSORS DATASUBS IE AGAM GAM RM RC RINV
           FUNNAMES SYMBOLS UPIE LPOWER UPOWER WORKARR WV LIND LIJ LIJK
           LIJKL LAB LABC LABCD MESS MARKERS LMARGIN CLOCK
           VELO EXPA ATIDA VTIDA ACCE SHEAR ROT ELWEYL
           MAGWEYL VMATRIX UVELO LVELO METRIC INVMETRIC CHRIS UACCE
           LACCE ROTDD ROTDU SHEARDD SHEARDU PROJDD PROJDU DETEM OUTINP)
    (MAPC (OBLIST) (FUNCTION RESETVARS))
    (SETQ CLOCK (TIME))
    (SETQ VELO (LIST 0 0 0 0))
    (SETQ RMARGIN 72)
    (SETQ LMARGIN  8)
% LMARGIN IS THE MARGIN FOR CONTINUATION LINES ONLY
    (SETQ LIND (LIST 0 1 2 3))
    (SETQ LIJ (LIST NIL NIL))
    (SETQ LIJK (LIST NIL NIL NIL))
    (SETQ LIJKL (LIST NIL NIL NIL NIL))
    (SETQ LAB (LIST NIL NIL))
    (SETQ LABC (LIST NIL NIL NIL))
    (SETQ LABCD (LIST NIL NIL NIL NIL))
    (ELLISINIT LIS 16)
    (RPLACD LIS NIL)
    (SETQ RC (GENSYM))
    (MKARRAY RC (LIST 4 4))
    (INVAR EMATRIX 4)
    (AND (EQ STOP (QUOTE DETERMINANT)) (GO END))
    (UPANDTRANS)
    (AND (EQ STOP (QUOTE IE)) (GO END))
    (MAKEMETRIC)
    (MAKEINVMETRIC)
    (OR (AGAMMA) (GO END))
    (AND (EQ STOP (QUOTE AGAMMA)) (GO END))
    (GAMMA)
    (AND (EQ STOP (QUOTE GAMMA)) (GO END))
    (MAKECHRISTOFFEL)
    (AND (EQ STOP (QUOTE CHRISTOFFEL)) (GO END))
    (MAKELANDLAGR)
 END (LINELENGTH 72)
    (TERPRI) (TERPRI)
    (MESSAGE (QUOTE ("I REALLY LIKED THIS!" "CAN I HAVE MORE ?"
                                           "  PLEASE ?!?")) T)
    (TERPRI)
    (MESSAGE (QUOTE "END OF WORK") T)
    (MESSAGE (LIST (QUOTE "RUN TIME =") (PLUS (TIME) (MINUS CLOCK))
                   (QUOTE MSEC)) T)
     ))
(de SINGLELANG (A B) (PROG (V W)
    (RPLACA (CDR LABC) A)
    (RPLACA (CDR LB) A)
    (RPLACA (CDDR LIJK) B)
    (RPLACA (CDDR LB) B)
    (MAPC LIND (FUNCTION (LAMBDA (K) (PROGN
        (RPLACA LIJK K)
        (RPLACA (CDDR LABC) K)
        (RPLACA LA K)
        (RPLACA (CDDR LA) K)
        (MAPC LIND (FUNCTION (LAMBDA (L) (PROGN
            (RPLACA (CDR LIJK) L)
            (RPLACA LABC L)
            (RPLACA (CDR LA) L)
            (RPLACA LB L)
            (SETQ W (STIMES (LIST (PICKEL CHRIS LIJK)
                                  (PICKEL CHRIS LABC) )))
            (OR (EQN W 0) (SETQ V (CONS W V)))
            (SETQ W (STIMES (LIST -1 (PICKEL CHRIS LA)
                                  (PICKEL CHRIS LB) )))
            (OR (EQN W 0) (SETQ V (CONS W V))) )))) ))))
    (SETQ V (COND ((NULL V)   0)
                  ((NULL (CDR V))   (CAR V))
                  (T   (SPLUS V)) ))
    (PUTIN RC LAB V)
    (RETURN V) ))
(de MAKELANDLAGR ( ) (PROG (V W LA LB)
    (SETQ LA (LIST NIL NIL NIL))
    (SETQ LB (LIST NIL NIL NIL))
    (MAPC LIND (FUNCTION (LAMBDA (A) (PROG2
        (RPLACA LAB A)
        (MAPC (MEMBER A LIND) (FUNCTION (LAMBDA (B) (PROGN
            (RPLACA (CDR LAB) B)
            (SETQ W (LIST (PICKEL INVMETRIC LAB) (SINGLELANG A B)))
            (COND ((MEMBER 0 W)   (SETQ W 0))
                  (T   (PROGN (SETQ W (CONS DETEM W))
                              (OR (EQN A B) (SETQ W (CONS 2 W)))
                              (SETQ W (STIMES W)) )))
            (OR (EQN W 0) (SETQ V (CONS W V))) )))) ))))
    (SETQ V (COND ((NULL V)   0)
                  ((NULL (CDR V))   (CAR V))
                  (T   (SPLUS V)) ))
    (SUBSALL (QUOTE V) (QUOTE LANDLAGR) NIL NIL SIMPLSUBS)
    (INPRINT (QUOTE LANDLAGR) NIL NIL (QUOTE =) V)
    (RETURN V) ))
